roundingwarnings = off;
display = decimal;

prec = 512;

signum = proc(f) {
    var s;
    if (f == 0) then s = 0 else s = f / abs(f);
    return s;
};

hex = proc(m) {
    var d, s, p, n, i;
    d = 16;
    A = [|"0","1","2","3","4","5","6","7","8","9","a","b","c","d","e","f"|];
    s = "";
    p = "0x";
    n = 1;
    while (n <= 32) do {
        i = mod(m, d);
        s = A[i] @ s;
        m = div(m, d);
        n = n + 1;
    };
    return p @ s;
};

repr = proc(s, m) {
    var z, n, r;
    B = 2^128;
    if (s < 0) then m = 2^512 - m;
    z = "";
    n = 1;
    while (n <= 4) do {
        r = hex(mod(m, B));
        z = r @ ",\n" @ z;
        m = div(m, B);
        n = n + 1;
    };
    return z;
};

P = 492;
X = 2^P;

// --- Polynomial approximating sine -----------------------------------------

SIN = sin(x);
N = 30;
T = taylor(SIN, 2*N+1, 0);
print("");
while (N >= 0) do {
    var n, c, s, m;
    n = 2 * N + 1;
    c = coeff(T, n);
    s = signum(c);
    m = nearestint(abs(c) * X);
    print("//", s @ " / " @ n @ "! â‰ˆ");
    print("FP492::new(");
    print(repr(s, m), "),");
    N = N - 1;
};
